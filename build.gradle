import java.text.SimpleDateFormat

/*
 * Licensed to Miguel Arregui ("marregui") under one or more contributor
 * license agreements. See the LICENSE file distributed with this work
 * for additional information regarding copyright ownership. You may
 * obtain a copy at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.  See the
 * License for the specific language governing permissions and limitations
 * under the License.
 *
 * Copyright (c) 2019 - 2022, Miguel Arregui a.k.a. marregui
 */

plugins {
    id 'application'
    id 'net.nemerosa.versioning' version '2.15.0'
    // https://plugins.gradle.org/plugin/net.nemerosa.versioning
}

group = 'io.quest'
version = '1.0.0'
description = 'Desktop user interface to QuestDB through the Postgres Wire Protocol.'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation 'org.questdb:questdb:7.0.1'
    implementation 'org.postgresql:postgresql:42.5.4'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.realityforge.org.jetbrains.annotations:org.jetbrains.annotations:1.7.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-commons:1.8.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:4.3.1'
}

application {
    mainClass = 'io.quest.Quest'
    applicationDefaultJvmArgs = ['-Xmx3G', '-server', '-Dfile.encoding=UTF-8']
}

jar {
    manifest {
        attributes(
                'Main-Class': 'io.quest.Quest',
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Branch': versioning.info.branch,
                'Build-Revision': versioning.info.commit,
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})"
        )
    }
}

distributions.main {
    contents {
        from startScripts
    }
}

startScripts {
    startScripts.applicationName = 'quest'
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('set APP_HOME=%DIRNAME%..', 'set APP_HOME=%DIRNAME%')
        unixScriptFile.text = unixScriptFile.text.replace('APP_HOME=$( cd "${APP_HOME:-./}.." && pwd -P ) || exit', 'APP_HOME=$(pwd -P) || exit')
    }
}

distZip {
    eachFile { file ->
        if (file.path.contains('bin')) {
            file.exclude()
        }
    }
}

distTar {
    eachFile { file ->
        if (file.path.contains('bin')) {
            file.exclude()
        }
    }
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = '8.0.1'
}
